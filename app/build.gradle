plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'com.zjutjh.ijh'
    compileSdk compile_sdk

    defaultConfig {
        applicationId "com.zjutjh.ijh"
        minSdk min_sdk
        targetSdk target_sdk

        versionCode 4
        versionName '0.2.0'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        benchmark {
            initWith buildTypes.release
            matchingFallbacks = ['release']
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'benchmark-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlinOptions {
        freeCompilerArgs = freeCompilerArgs + "-opt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
        viewBinding true
        buildConfig true
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Androidx Core & UI
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.core:core-animation:1.0.0'
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation 'androidx.compose.material3:material3:1.3.1'
    implementation 'androidx.compose.material:material-icons-extended:1.7.6'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.navigation:navigation-compose:2.8.5'
    implementation 'androidx.profileinstaller:profileinstaller:1.4.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.36.0'

    // For building AppWidgets with Glance
    def glance_version = '1.1.1'
    implementation "androidx.glance:glance-appwidget:$glance_version"
    implementation "androidx.glance:glance-material3:$glance_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.8'

    implementation project(":core:data")
    implementation project(":core:common")

    implementation 'com.squareup.moshi:moshi:1.15.2'

    // Work
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Dependency injection (Hilt)
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}
